// The filter in this effect (sections commented //f)
//Copyright (C) 2004 and later Cockos Incorporated
// License: GPL - http://www.gnu.org/licenses/gpl.html
// The rest ashcat_lt@hotmail.com 2013 Rights Are Lies!

desc: sine amplifier

slider1:0<-120,60,.1> limit/period (dB)
slider2:0<-120,60,.1> output volume (dB)


@slider
period = 2 ^ (slider1/6);
gain = 2 ^ (slider2/6);


@init
  damp=4.01;//f
  c = 1/tan($pi*18000/srate);//f
  fk = 1 / (1 + c*(c+damp)); //f
  fa1 = 2 * (1 - c*c) * fk;//f
  fa0 = (1 + c*(c-damp)) * fk;//f
  //damp!=oldamp ? fd1l=fd2l=fd1r=fd2r=0;//f
  oldamp=damp; //f

@sample
  l_samp = gain * period * sin ((spl0 / period) * ($pi/2));
  r_samp = gain * period * sin ((spl1 / period) * ($pi/2));
      
fd0l = (fk*l_samp) - (fa1*fd1l) - (fa0*fd2l);//f
fd0r = (fk*r_samp) - (fa1*fd1r) - (fa0*fd2r);//f

spl0 = fd0l + fd1l + fd1l + fd2l;//f
spl1 = fd0r + fd1r + fd1r + fd2r;//f

fd2l = fd1l;//f
fd2r = fd1r;//f

fd1l = fd0l;//f
fd1r = fd0r;//f
